/*
 * Copyright (C) 2016 BROADSoftware
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */


plugins {
  id 'nebula.ospackage' version '3.4.0'
}
 
apply plugin: 'java'
apply plugin: 'eclipse'


defaultTasks 'rpm'


repositories {
    // Use 'jcenter' for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
}

dependencies {
    compile "org.slf4j:slf4j-api:$sl4jApiVersion"
    compile "org.slf4j:slf4j-log4j12:$sl4jLog4jVersion"
    compile "log4j:log4j:$log4jVersion"
    compile "org.apache.kafka:kafka-clients:$kafkaClientVersion"    
 	compile "net.sf.jopt-simple:jopt-simple:$joptVersion"
    testCompile "junit:junit:$junitVersion"
}


//create a single Jar with all dependencies
task fatJar(type: Jar) {
	manifest {
        attributes 'Implementation-Title': 'ktail', 
        	'Implementation-Version': version,
        	'Main-Class': 'com.kappaware.ktail.Main'
    }
    baseName = project.name
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

task rpm(type: Rpm) {
	summary 'Dump kafka topic'
	packageName 'ktail'
	version versionForRpm
	arch NOARCH
	vendor 'BROADSoftware'
	license 'Apache 2.0'
	os LINUX
	release '1'
	packageDescription 'ktail is a small tool intended to dump content of a Kafka topic from a given time.'
    
   	from(fatJar.outputs.files) {
	    into '/opt/ktail'
	    user 'root'
		group 'root'
		//rename "${fatJarBaseName}(.+).jar", "${fatJarBaseName}.jar"
	    fileMode 0644
    }
    
   	from('scripts/packages/ktail') { into '/usr/bin'; user 'root'; group 'root'; fileMode 0755  }
   	from('scripts/packages/log4j.xml') { into '/etc/ktail'; user 'root'; group 'root'; fileMode 0644  }
   	from('scripts/packages/setenv.sh') { into '/etc/ktail'; user 'root'; group 'root'; fileMode 0644    }
   	from('scripts/packages/kafka_client_jaas.conf') { into '/etc/ktail'; user 'root'; group 'root'; fileMode 0644    }
}




